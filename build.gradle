plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "maven-publish"
    id "java"
    id "checkstyle"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk.output.cassandra"
version = "0.4.0"
description = "Cassandra output plugin for Embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8


embulkPlugin {
    mainClass = "org.embulk.output.kafka.CassandraOutputPlugin"
    category = "output"
    type = "cassandra"
}

dependencies {
    compileOnly  "org.embulk:embulk-core:0.9.23"
    compile("com.datastax.cassandra:cassandra-driver-core:3.10.+") {
        exclude module: 'netty-buffer'
        exclude module: 'netty-codec'
        exclude module: 'netty-common'
        exclude module: 'netty-handler'
        exclude module: 'netty-transport'
        exclude module: 'guava'
        exclude module: 'slf4j-api'
        exclude module: 'jackson-databind'
        exclude module: 'jackson-core'
    }

    compile  "io.netty:netty-common:4.0.44.Final"
    compile  "io.netty:netty-buffer:4.0.44.Final"

    compile  "io.netty:netty-codec:4.0.44.Final"
    compile  "io.netty:netty-handler:4.0.44.Final"
    compile  "io.netty:netty-transport:4.0.44.Final"

    testCompile "junit:junit:4.+"
    testCompile  "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile  "org.embulk:embulk-deps-config:0.9.23"
    testCompile  "org.embulk:embulk-test:0.9.23"
    testCompile  "org.embulk:embulk-standards:0.9.23"
    testCompile  "org.cassandraunit:cassandra-unit:3.11.2.0"
}

test {
    minHeapSize = "512m"
    maxHeapSize = "4g"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE.txt")
    authors = [ "joker1007" ]
    email = [ "kakyoin.hierophant@gmail.com" ]
    description = "Cassandra output plugin for Embulk"
    summary = "Cassandra output plugin for Embulk"
    homepage = "https://github.com/joker1007/embulk-output-cassandra"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

