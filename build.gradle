plugins {
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "maven-publish"
    id "java"
    id "checkstyle"
}
repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk.output.cassandra"
version = "0.5.0"
description = "Cassandra output plugin for Embulk"

sourceCompatibility = 1.8
targetCompatibility = 1.8


embulkPlugin {
    mainClass = "org.embulk.output.cassandra.CassandraOutputPlugin"
    category = "output"
    type = "cassandra"
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"
    implementation("com.datastax.cassandra:cassandra-driver-core:3.10.+") {
        exclude module: 'netty-buffer'
        exclude module: 'netty-codec'
        exclude module: 'netty-common'
        exclude module: 'netty-handler'
        exclude module: 'netty-transport'
        exclude module: 'slf4j-api'
        exclude module: 'jackson-databind'
        exclude module: 'jackson-core'
    }
    implementation  "io.netty:netty-common:4.0.44.Final"
    implementation  "io.netty:netty-buffer:4.0.44.Final"

    implementation  "io.netty:netty-codec:4.0.44.Final"
    implementation  "io.netty:netty-handler:4.0.44.Final"
    implementation  "io.netty:netty-transport:4.0.44.Final"

    implementation("org.embulk:embulk-util-config:0.3.4") {
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.7"

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-spi:0.11"
    testImplementation "org.embulk:embulk-core:0.11.0"
    testImplementation "org.embulk:embulk-junit4:0.11.0"
    testImplementation "org.embulk:embulk-input-file:0.11.0"
    testImplementation "org.embulk:embulk-parser-csv:0.11.0"
    testImplementation "org.embulk:embulk-deps:0.11.0"

    testImplementation("org.cassandraunit:cassandra-unit:3.11.2.0") {
        exclude module: 'guava'
    }
}

test {
    minHeapSize = "512m"
    maxHeapSize = "4g"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE.txt")
    authors = [ "joker1007" ]
    email = [ "kakyoin.hierophant@gmail.com" ]
    description = "Cassandra output plugin for Embulk"
    summary = "Cassandra output plugin for Embulk"
    homepage = "https://github.com/joker1007/embulk-output-cassandra"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

